# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
pkgbase=glibc
pkgname=(glibc)
pkgver=2.33
pkgrel=5
arch=(x86_64)
url='https://www.gnu.org/software/libc'
license=(GPL LGPL)
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz{,.sig}
        locale.gen.txt
        locale-gen
        sdt.h sdt-config.h
        bz27343.patch
        0001-nptl_db-Support-different-libpthread-ld.so-load-orde.patch
        0002-nptl-Check-for-compatible-GDB-in-nptl-tst-pthread-gd.patch
        0003-nptl-Do-not-build-nptl-tst-pthread-gdb-attach-as-PIE.patch)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

prepare() {
  mkdir -p glibc-build lib32-glibc-build

  [[ -d glibc-$pkgver ]] && ln -s glibc-$pkgver glibc 
  cd glibc
  patch -p1 -i "$srcdir"/bz27343.patch
  patch -p1 -i "$srcdir"/0001-nptl_db-Support-different-libpthread-ld.so-load-orde.patch
  patch -p1 -i "$srcdir"/0002-nptl-Check-for-compatible-GDB-in-nptl-tst-pthread-gd.patch
  patch -p1 -i "$srcdir"/0003-nptl-Do-not-build-nptl-tst-pthread-gdb-attach-as-PIE.patch
}

build() {
  local _configure_flags=(
      --prefix=/
      --with-headers=/include
      --with-bugurl=https://bugs.archlinux.org/
      --enable-add-ons
      --enable-bind-now
      --enable-cet
      --enable-kernel=4.4
      --enable-lock-elision
      --enable-multi-arch
      --enable-stack-protector=strong
      --enable-stackguard-randomization
      --enable-static-pie
      --enable-systemtap
      --disable-profile
      --disable-werror
  )

  cd "$srcdir/glibc-build"
  echo "slibdir=/lib" >> configparms
  echo "rtlddir=/lib" >> configparms
  echo "sbindir=/bin" >> configparms
  echo "rootsbindir=/bin" >> configparms
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}
  LDFLAGS=${LDFLAGS/,-z,now/}

  "$srcdir/glibc/configure" \
      --libdir=/lib \
      --libexecdir=/lib \
      ${_configure_flags[@]}
  echo "build-programs=no" >> configparms
  make

# re-enable fortify for programs
  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CC += -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -D_FORTIFY_SOURCE=2" >> configparms
  make
}

check() {
  cd glibc-build
  sed -i '/FORTIFY/d' configparms
  make check || true
}

package_glibc() {
  pkgdesc='GNU C Library'
  install=glibc.install
  install -dm755 "$pkgdir/etc"
  touch "$pkgdir/etc/ld.so.conf"
  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/etc/ld.so.{cache,conf}
  rm -f "$pkgdir"/bin/{tzselect,zdump,zic}

  cd glibc
  install -dm755 "$pkgdir"/lib/{locale,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/etc/nscd.conf"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/var/db/nscd"
  install -m644 posix/gai.conf "$pkgdir"/etc/gai.conf
  install -m755 "$srcdir/locale-gen" "$pkgdir/bin"
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc/localedata/SUPPORTED" >> "$pkgdir/etc/locale.gen"

  if check_option 'debug' n; then
    find "$pkgdir"/bin -type f -executable -exec strip $STRIP_BINARIES {} + 2> /dev/null || true
    find "$pkgdir"/lib -name '*.a' -type f -exec strip $STRIP_STATIC {} + 2> /dev/null || true
    find "$pkgdir"/lib \
      -not -name 'ld-*.so' \
      -not -name 'libc-*.so' \
      -not -name 'libpthread-*.so' \
      -not -name 'libthread_db-*.so' \
      -name '*-*.so' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true
  fi

  install -Dm644 "$srcdir/sdt.h" "$pkgdir/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/include/sys/sdt-config.h"
  rm -f "$pkgdir"/include/crypt.h "$pkgdir"/lib/libcrypt.{a,so}
}
